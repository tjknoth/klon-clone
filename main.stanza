; Generated by JITX 3d1bcc2e2f2fdd1dcb4a955277db5924636666a7
#use-added-syntax(jitx)
defpackage main :
  import core
  import jitx
  import jitx/commands
  import ocdb/utils/generic-components
  import helpers

; carl notes:
; can try: film capacitor, ptfe capacitor
; c15: x7r (and other big ones)
; small caps: c0g
; op amp: 
; gain bandwidth

;TODOs:
;-two op amps in main module, connect them to input/gain and sum/tone respectively

; Define the shape/size of the board
val board-shape = RoundedRectangle(30.0, 18.5, 0.25)

; Setup the board
defn setup-board () :
  set-board(ocdb/utils/defaults/default-board(ocdb/manufacturers/stackups/jlcpcb-jlc2313, board-shape))
  set-rules(ocdb/manufacturers/rules/jlcpcb-rules)

; Input stage 
pcb-module input :
  pin gnd
  pin power-4v5
  pin power-9v
  pin out
  pin opamp-neg
  pin opamp-pos
  pin opamp-out


  ; Components: input jack header, two resistors and a cap
  inst jack : pin-header(3)
  inst r1 : chip-resistor(10.0e3)
  inst r2 : chip-resistor(1.0e6)
  inst c1 : ceramic-cap(0.1e-6)

  net (jack.p[3], r1.p[1])
  ; jack.p[2] not used
  net (jack.p[1], gnd)
  net (r1.p[2], c1.p[1])
  net (c1.p[2], r2.p[1])
  net (r2.p[2], power-4v5)
  net (r2.p[1], opamp-pos)
  net (opamp-out, out)
  net (out, opamp-neg)

  ;schematic-group(self) = input

; Bypass mode
pcb-module bypass :
  pin in
  pin out
  pin gnd

  inst c2 : ceramic-cap(4.7e-6)
  inst r3 : chip-resistor(100.0e3)
  inst r4 : chip-resistor(560.0)

  net (in, c2.p[1])
  net (c2.p[2], r3.p[1])
  net (r3.p[2], gnd)
  net (r3.p[1], r4.p[1])
  net (r4.p[2], out)

; opamp gain
pcb-module gain :
  pin in
  pin out
  pin gnd
  pin power-4v5
  pin power-9v 
  pin opamp-neg
  pin opamp-pos
  pin opamp-out

  ; TODO: this is 10k! need the right one
  inst rv2 : database-part(["mpn" => "RK1631110U1Q", "manufacturer" => "ALPSALPINE"]) 

  ; TODO: everything

; summing opamp
pcb-module sum-amp :
  pin in
  pin out
  pin gnd
  pin power-n8v6
  pin power-16v2
  pin power-4v5
  pin opamp-neg
  pin opamp-pos
  pin opamp-out

  ; components: 1 resistor, 1 cap
  inst r20 : chip-resistor(392.0e3)
  inst c13 : ceramic-cap(820.0e-12)

  ; nets: all in parallel
  net (in, opamp-neg)
  net (power-4v5, opamp-pos)
  net (in, r20.p[1])
  net (in, c13.p[1])
  net (r20.p[2], out)
  net (c13.p[2], out)
  net (opamp-out, out)

  ;schematic-group(self) = sum-amp

; tone control
pcb-module tone :
  pin in
  pin out
  pin gnd
  pin power-4v5
  pin power-16v2
  pin power-n8v6
  pin opamp-neg
  pin opamp-pos
  pin opamp-out

  ; Components
  inst r21 : chip-resistor(1.8e3)
  inst r22 : chip-resistor(100.0e3)
  inst r23 : chip-resistor(4.7e3)
  inst r24 : chip-resistor(100.0e3)
  inst c14 : ceramic-cap(3.9e-9)
  ; 10k linear taper pot
  inst rv2 : database-part(["mpn" => "RK1631110U1Q", "manufacturer" => "ALPSALPINE"]) 

  ; nets
  net (in, r21.p[1])
  net (in, r22.p[1])
  net (r21.p[2], rv2.p[1])
  net (rv2.p[3], r23.p[1])
  net (r23.p[2], out)
  net (rv2.p[2], c14.p[1])
  net (c14.p[2], r22.p[2])
  net (r22.p[2], opamp-neg)
  net (power-4v5, opamp-pos)
  net (opamp-out, out)
  net (c14.p[2], r24.p[1])
  net (r24.p[2], out)

  ;schematic-group(self) = tone

; power supply
pcb-module power :
  pin gnd
  pin power-9v
  pin power-4v5
  pin power-n8v6
  pin power-16v2

  ; capacitors
  inst c17 : ceramic-cap(47.0e-6)
  inst c18 : ceramic-cap(47.0e-6)
  inst c19 : ceramic-cap(1.0e-6)
  inst c20 : ceramic-cap(1.0e-6)
  inst c21 : ceramic-cap(1.0e-6)
  inst c22 : ceramic-cap(1.0e-6)
  ; resistors
  inst r29 : chip-resistor(27.0e3)
  inst r30 : chip-resistor(27.0e3)
  ; Zener diode
  ; A is base of triangle
  ; C/K is tip
  inst d4 : database-part(["mpn" => "1N4742A", "manufacturer" => "Tak Cheong"])
  ; Normal diodes
  inst d7 : database-part(["mpn" => "1N4001-E3/73", "manufacturer" => "Vishay"]) 
  inst d8 : database-part(["mpn" => "1N4001-E3/73", "manufacturer" => "Vishay"]) 
  ; DC power jack
  inst j3 : database-part(["mpn" => "DC-060A", "manufacturer" => "HRO Electronics"])
  ; Voltage converter 
  inst u3 : database-part(["mpn" => "MAX1044ESA+T", "manufacturer" => "Analog Devices"])

  ; Connect power, diode, capacitor c17
  net (gnd, j3.CON1)
  net (j3.CON2, d4.K)
  net (d4.A, gnd)
  net (c17.p[1], gnd)
  net (c17.p[2], j3.CON2)
  net (power-9v, j3.CON2)

  ; 4v5 sub-circuit
  net (power-9v, r29.p[1])
  net (r29.p[2], r30.p[1])
  net (r30.p[2], gnd)
  net (r29.p[2], power-4v5)
  net (r29.p[2], c18.p[1])
  net (c18.p[2], gnd)

  ; Converter sub-circuit
  net (u3.N_C_BOOST, power-9v)
  net (u3.N_C_BOOST, d7.A)
  net (d7.K, c20.p[2])
  net (c20.p[2], d8.A)
  net (d8.K, power-16v2)
  net (d8.K, c21.p[1])
  net (c21.p[2], gnd)
  net (u3.CAP+, c20.p[1])
  net (u3.GND, gnd) 
  net (u3.CAP-, c22.p[1])
  net (c22.p[2], c20.p[1])
  net (u3.V+, power-9v)
  net (u3.VOUT, power-n8v6)
  net (u3.VOUT, c19.p[1])
  net (c19.p[2], gnd)

  ;schematic-group(self) = power


pcb-module output :
  pin gain-in
  pin bypass-in
  pin gnd

  ; panel mount jack and SPDT switch
  ; jack: p1 = tip, p2 = connector, p3 = shoulder
  ; switch: p1,2 = throws 
  inst jack : pin-header(3)
  inst sw1 : pin-header(3)
  ; audio taper 10k pot
  inst rv3 : database-part(["mpn" => "RK1631110TT3", "manufacturer" => "ALPSALPINE"])
  ; capacitors and resistors
  inst c15 : ceramic-cap(4.7e-6)
  inst r25 : chip-resistor(560.0)
  inst r243 : chip-resistor(68.0e3)
  inst r242 : chip-resistor(68.0e3)
  inst r28 : chip-resistor(100.0e3)

  ; nets
  net (gain-in, c15.p[1])
  net (c15.p[2], r25.p[1])
  net (r25.p[2], rv3.p[1])
  net (rv3.p[2], sw1.p[2])
  net (rv3.p[3], gnd)
  net (bypass-in, r243.p[1])
  net (bypass-in, sw1.p[1])
  net (r243.p[2], sw1.p[3])
  net (r242.p[2], sw1.p[3])
  net (sw1.p[3], jack.p[1])
  net (r28.p[1], jack.p[2])
  net (r28.p[1], jack.p[3])
  net (r28.p[2], gnd)



; Module to run as a design
pcb-module centaur :
  ; define some pins/ports
  pin gnd

  ; instantiate all sub-modules
  inst input : input
  inst gain : gain
  inst bypass : bypass
  inst sum-amp : sum-amp
  inst tone : tone
  inst power : power
  inst output : output

  ; instantate op-amps
  inst u1 : database-part(["mpn" => "TL072IDT", "manufacturer" => "STMicroelectronics"])
  inst u2 : database-part(["mpn" => "TL072IDT", "manufacturer" => "STMicroelectronics"])

  ; net 9v op-amp
  net (u1.VCC+, power.power-9v)
  net (u1.VCC-, gnd)
  net (u1.IN1+, input.opamp-pos)
  net (u1.IN1-, input.opamp-neg)
  net (u1.OUT1, input.opamp-out)
  net (u1.IN2+, gain.opamp-pos)
  net (u1.IN2-, gain.opamp-neg)
  net (u1.OUT2, gain.opamp-out)

  ; net 16v op-amp
  net (u2.VCC+, power.power-16v2)
  net (u2.VCC-, power.power-n8v6)
  net (u2.IN1+, sum-amp.opamp-pos)
  net (u2.IN1-, sum-amp.opamp-neg)
  net (u2.OUT1, sum-amp.opamp-out)
  net (u2.IN2+, tone.opamp-pos)
  net (u2.IN2-, tone.opamp-neg)
  net (u2.OUT2, tone.opamp-out)

  ; Net sub-modules
  net (input.out, gain.in)
  net (input.out, bypass.in)
  net (gain.out, sum-amp.in)
  net (sum-amp.out, tone.in)
  net (tone.out, output.gain-in)
  net (bypass.out, output.bypass-in)

  ; net gnds 
  net GND (input.gnd bypass.gnd gain.gnd tone.gnd power.gnd output.gnd)  
  symbol(GND) = ocdb/utils/symbols/ground-sym

  ; Net power module to all sub-modules
  net (power.power-9v, input.power-9v)
  net (power.power-9v, gain.power-9v)
  net (power.power-4v5, input.power-4v5)
  net (power.power-4v5, gain.power-4v5)
  net (power.power-4v5, sum-amp.power-4v5)
  net (power.power-4v5, tone.power-4v5)
  net (power.power-n8v6, sum-amp.power-n8v6)
  net (power.power-n8v6, tone.power-n8v6)
  net (power.power-16v2, sum-amp.power-16v2)
  net (power.power-16v2, tone.power-16v2)


  ; Write the board version on silkscreen
  ;inst version-label  : ocdb/artwork/board-text/version-silkscreen("Version 1.0")
  ;place(version-label) at loc(0.0, height(board-shape) / 2.0 - 1.0) on Bottom

; Set the design name - a directory with this name will be generated under the "designs" directory
set-current-design("jitx-design")

; Set the schematic sheet size
set-paper(ANSI-A)

; Setup the board properties
setup-board()

; Set the top level module (the module to be compile into a schematic and PCB)
set-main-module(centaur)

; Use any helper function from helpers.stanza here
; run-check-on-design(centaur)

; View the results
view-board()
view-schematic()
view-design-explorer()
